var canvas = document.getElementById("canvas")
var ctx = canvas.getContext("2d")

# initialize game sprites
var backgroundSprite = new Image()
var mineSprite = new Image()
var flagSprite = new Image()
var tileSprite = new Image()

# load game sprites
backgroundSprite.src = "img/background.jpg"
mineSprite.src = "img/mine.jpg"
flagSprite.src = "img/flag.jpg"
tileSprite.src = "img/tile.jpg"



# Tile class
class Tile:
    def Tile(size, x, y):
        self.size = size
        self.x = x * self.size
        self.y = y * self.size
        self.isHidden = true
        self.isMine = false
        self.isFlagged = false
        self.numberOfAdjacentMines = 0
        self.wasSearched = false
    end

    def draw():
        var x = self.x
        var y = self.y

        if self.isHidden:
            if self.isFlagged:
                # unclicked flagged
                ctx.drawImage(flagSprite, x, y)
            else:
                # unclicked
                ctx.drawImage(tileSprite, x, y)
            end
        else:
            # background
            ctx.drawImage(backgroundSprite, x, y)
        end

        # if tile uncovered
        if not self.isHidden:
            if self.numberOfAdjacentMines != 0:
                ctx.fillStyle = "#333"
                ctx.font = "15px 'BebasNeueRegular', 'Arial', sans-serif"
                ctx.fillText(self.numberOfAdjacentMines,
                             x + 9, y + self.size - 5)
            end

            if self.isMine:
                ctx.drawImage(mineSprite, x, y)
            end
        end
    end
end


class Board:
    def Board(width, height, tileSize):
        self.width = width
        self.height = height
        self.tileSize = tileSize
        self.tiles = []
        self.numberOfHiddenTiles = 0
    end

    # initialize the board
    def init():
        # initialize with all tiles hidden
        self.numberOfHiddenTiles = self.width * self.height

        # Initialize the board array
        for i in [0 .. self.width-1]:
            self.tiles[i] = []
            for j in [0 .. self.height-1]:
                self.tiles[i][j] = new Tile(self.tileSize, i, j)
            end
        end
    end

    # Draw every tile
    def draw():
        for i in [0 .. self.width-1]:
            for j in [0 .. self.height-1]:
                self.tiles[i][j].draw()
            end
        end
    end

    # Reveal the whole board
    def revealAll():
        for i in [0 .. self.width-1]:
            for j in [0 .. self.height-1]:
                self.tiles[i][j].isHidden = false
            end
        end

        self.draw()
    end

    # Randomly scatter mines on the field
    def addMines(numberOfMines, mouseX, mouseY):
        var x = Math.floor(Math.random() * self.width)
        var y = Math.floor(Math.random() * self.height)
        for i in [0 .. numberOfMines-1]:
            # check if already mine
            while (self.tiles[x][y].isMine) or ((x == mouseX) and (y == mouseY)):
                x = Math.floor(Math.random() * self.width)
                y = Math.floor(Math.random() * self.height)
            end
            self.tiles[x][y].isMine = true
        end

        self.setAdjacentMines()
    end

    # Compute the number of adjacent mines on the whole board
    def setAdjacentMines():
        for i in [0 .. self.width-1]:
            for j in [0 .. self.height-1]:
                self.computeAdjacentMines(i, j)
            end
        end
    end

    # Compute the number of adjacent mines of tile x, y
    def computeAdjacentMines(x, y):
        var width = self.tiles.length
        var height = self.tiles[0].length
        var count = 0

        for i in [-1 .. 1]:
            for j in [-1 .. 1]:
                # inside canvas ?
                if (x + i >= 0) and (x + i < width) and (y + j >= 0) and (y + j < height):
                    # is a mine ?
                    if self.tiles[x + i][y + j].isMine:
                        count += 1
                    end
                end
            end
        end

        self.tiles[x][y].numberOfAdjacentMines = count
    end

    # Reveal all empty tiles around the one clicked
    def reveal(xInit, yInit):
        # reveal the tile
        var clickedTile = self.tiles[xInit][yInit]
        if clickedTile.isHidden:
            clickedTile.isHidden = false
            self.numberOfHiddenTiles -= 1
        end
        clickedTile.draw()

        # if it is empty, reveal around
        if self.tiles[xInit][yInit].numberOfAdjacentMines == 0:
            var clickedArr = self.revealAroundTile([xInit, yInit])
            self.recursiveReveal(clickedArr)
        end

        self.draw()
    end

    # Reveal the tiles surrounding a empty one if they are not mines
    def revealAroundTile(coords):
        var x = coords[0]
        var y = coords[1]
        var width = self.tiles.length
        var height = self.tiles[0].length
        var currentTile = self.tiles[x][y]
        var tilesToClear = []

        currentTile.wasSearched = true

        if currentTile.numberOfAdjacentMines == 0:
            for i in [-1 .. 1]:
                for j in [-1 .. 1]:
                    # inside canvas ?
                    if (x + i >= 0) and (x + i < width) and (y + j >= 0) and (y + j < height):
                        # is not a mine ?
                        currentTile = self.tiles[x + i][y + j]

                        # add tiles to clear to array
                        # empty tile
                        if currentTile.numberOfAdjacentMines == 0 and Math.abs(i + j) == 1 and not currentTile.wasSearched:
                            tilesToClear.push([x + i, y + j])
                        end

                        # show current tile
                        if not currentTile.isMine and currentTile.isHidden:
                            currentTile.isHidden = false
                            self.numberOfHiddenTiles -= 1
                        end
                    end
                end
            end
            self.tiles[x][y].isHidden = false
        end

        return tilesToClear
    end

    # Recursive function used to reveal empty tiles
    def recursiveReveal(tilesToClear):
        var arr = []
        var returned = []

        if tilesToClear.length == 0:
            returned = []
        else if tilesToClear.length == 1:
            # clear around this tile
            arr = self.revealAroundTile(tilesToClear[0])
            # and start again on the tiles around it
            returned = self.recursiveReveal(arr)
        else:
            var first = tilesToClear.shift()
            # clear around the first tile of the array and do it on the others
            arr = self.recursiveReveal([first]).concat(self.recursiveReveal(tilesToClear))
            returned = arr
        end

        return returned
    end
end


# Main game class
class Game:
    def Game(width, height, numberOfMines):
        self.width = width
        self.height = height
        self.numberOfMines = numberOfMines

        self.guiHeight = 25
        self.tileSize = 25

        self.board = new Board(self.width, self.height, self.tileSize)
        self.mines = []

        self.isFirstClick = true
        self.timer = {}
        self.time = 0
    end

    # trick to have the correct value of this in self.init
    def newGame(e):
        self.init(e)
    end

    # Game initialisation
    def init():
        # set up canvas
        canvas.width = self.width * self.tileSize
        canvas.height = self.height * self.tileSize + self.guiHeight

        # add mouse support
        canvas.onmousedown = def (e): self.handleClick.call(self, e) end

        self.isFirstClick = true

        # initialize time
        self.time = 0

        self.board.init()

        self.board.draw()
        self.drawGUI("Game started, waiting for click...")

        tileSprite.onload = def ():
            self.board.draw()
        end
    end

    # Timer
    def startTimer():
        self.drawGUI("Time: 0")

        def onTick():
            self.drawGUI("Time: " + self.time)
            self.time += 1
        end
        self.timer = setInterval(onTick, 1000)
    end

    # Draw game information on canvas
    def drawGUI(text):
        ctx.fillStyle = "#333"
        ctx.fillRect(0, canvas.height - self.guiHeight,
                     canvas.width, self.guiHeight)
        ctx.fillStyle = "#eee"
        ctx.font = "15px 'BebasNeueRegular', 'Arial', sans-serif"

        ctx.fillText(text, 7, canvas.height - 7)
    end

    # Click handler
    # See http://www.quirksmode.org/js/events_properties.html
    def handleClick(e):
        var mouseX = 0
        var mouseY = 0
        var clickedTile = 0
        var rightClick = 0

        # determine if right click
        if e.which:
            rightClick = (e.which == 3)
        else if e.button:
            rightClick = (e.button == 2)
        end

        # determine mouse position
        if e.offsetX:
            mouseX = e.offsetX
            mouseY = e.offsetY
        else if e.layerX:
            mouseX = e.layerX
            mouseY = e.layerY
        end

        # normalize by tile size to get the tile coordinates
        mouseX = Math.floor(mouseX / self.tileSize)
        mouseY = Math.floor(mouseY / self.tileSize)

        # if we click on the board
        if mouseY < self.board.tiles[0].length:
            clickedTile = self.board.tiles[mouseX][mouseY]

            if rightClick:
                clickedTile.isFlagged = not clickedTile.isFlagged
                clickedTile.draw()
            else if not clickedTile.isFlagged:
                # on first click, start timer and initialize
                # the mines for the player not to click on a mine
                if self.isFirstClick:
                    self.board.addMines(self.numberOfMines, mouseX, mouseY)
                    self.startTimer()
                    self.isFirstClick = false
                end

                if clickedTile.isMine:
                    # game lost
                    self.gameOver(false)
                else:
                    self.board.reveal(mouseX, mouseY)

                    if self.board.numberOfHiddenTiles == self.numberOfMines:
                        # game won
                        self.gameOver(true)
                    end
                end
            end
        end
    end

    # Called when all mines are found or when a mine is clicked
    def gameOver(won):
        # stop timer
        clearInterval(self.timer)

        if won:
            self.drawGUI("Congratulations! Score: " + self.time + "s. Click to play again")
        else:
            self.drawGUI("Game over! Click to play again")
        end

        # reveal the mines
        self.board.revealAll()

        canvas.onmousedown = def (e): self.newGame.call(self, e) end
    end
end

var game = 0
window.onload = def ():
    console.log("start")
    game = new Game(15, 15, 25)
    game.init()
end
